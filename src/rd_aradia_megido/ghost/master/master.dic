//************************************************************************
//							TABLE OF CONTENTS
//****************************************************************
//		As aradia's codebase expands I'll break this up into separate files but for now- where doin it like this man
//							BOOT FUNCTIONS
//							CLOSE FUNCTIONS
//							NETWORK FUNCTIONS
//							Shortcut FUNCTIONS
//							MENUS - Now found in menuMaster.dic & %(setlang)/menu.dic
//							AITALK
//************************************************************************
//							BOOT FUNCTIONS
//****************************************************************
setFirstBootVars
{
	//global variables default values
	setlang = "en" 
	onQuirk = 1
	nowQuirk = "alive"
	chatBalloon = 1
	gState = "default"
	bColour = "500"
	gKnow = IARRAY
	gMood = IARRAY
	//_mood = RAND(100)
	//_bootTime = year + "-" + month + "-" + day + "-" + hour + "," + _mood
	//gMood = (_bootTime,IARRAY)
	timeBlocks = IARRAY
	timeBlocks = ("dawn,5,7","morning,8,11","noon,12","afternoon,13,16","evening,17,19","night,20,23","midnight,24","postMidnight,1,4")
	userInfo = ("name,","username,","chatType,","species,","birthday,","gameState,","class,","aspect,","lunarSway,")
}

setBootVars
{
	//_mood = RAND(100)
	//_bootTime = year + "-" + month + "-" + day + "-" + hour + "," + _mood
	//gMood ,= (_bootTime)
}

GETUIINDEX
{
	_return = -1
	
	_i = 0
	while _return == -1 && _i < ARRAYSIZE(userInfo)
	{
		_currentArr = userInfo[_i]
		
		if TOLOWER(_argv[0]) == TOLOWER(_currentArr[0])
		{
			_return = _i
		}
		
		_i++
	}
	
	_return
}

//GET TIME BLOCK HOURS
GETTBHRS
{
	_return = -1
	
	_i = 0
	while _return == -1 && _i < ARRAYSIZE(timeBlocks)
	{
		_currentArr = timeBlocks[_i]
		
		if TOLOWER(_argv[0]) == TOLOWER(_currentArr[0])
		{
			_return = _i
		}
		
		_i++
	}
	
	_return
}

GETTBHoursFormatted
{
	_return = ""
	//get the hours for the current time block
	_pos = GETTBHRS(_argv[0])
	_timeBlock = timeBlocks[_pos]
	
	//if we got a valid result, lets format it!
	if _timeBlock != -1 
	{
		_i = 1
		
		while _i < ARRAYSIZE(_timeBlock)
		{
			//first loop
			if _i == 1
			{
				if _timeBlock[_i] == 12 
				{
					_return += "12:00"
					if ARRAYSIZE(_timeBlock) == 2
					{
						_return += "PM"
					}
				}
				elseif _timeBlock[_i] == 24
				{
					_return += "12:00"
					if ARRAYSIZE(_timeBlock) == 2
					{
						_return += "AM"
					}
				}
				elseif TOINT(_timeBlock[_i]) > 12
				{
					_civilianHour = TOINT(_timeBlock[_i]) - 12
					_return += "%_civilianHour:00"
					if ARRAYSIZE(_timeBlock) == 2
					{
						_return += "PM"
					}
				}
				else
				{
					_return += "%_timeBlock[_i]:00"
					if ARRAYSIZE(_timeBlock) == 2
					{
						_return += "AM"
					}
				}
			}
			else
			{
				if _timeBlock[_i] == 12 
				{
					_return += " - 12:00PM"
				}
				elseif _timeBlock[_i] == 24
				{
					_return += "- 12:00AM"
				}
				elseif TOINT(_timeBlock[_i]) > 12
				{
					_civilianHour = TOINT(_timeBlock[_i]) - 12
					if _civilianHour < 10
					{
						_return += " -  %_civilianHour:00PM"
					}
					else
					{
						_return += " - %_civilianHour:00PM"
					}
				}
				else
				{
					if TOINT(_timeBlock[_i]) < 10
					{
						_return += " -  %_timeBlock[_i]:00AM"
					}
					else
					{
						_return += " - %_timeBlock[_i]:00AM"
					}
				}
			}
			_i++
		}
	
		//if the block is a range, like "morning,5,7"
		if ARRAYSIZE(_timeBlock) == 3 
		{
			
		}
		else // the block is a single hour, like "noon,12"
		{
		}
	}
	_return
}

OnFirstBoot
{
	setFirstBootVars
	
	EVAL("%(setlang)FirstBoot")
}

enFirstBoot
{
	_time = hour + ":" + minute
	
	"\0\s[0]%sb(1)%sf(0)???? [??] began ???? %sf('306')\napocalypseArisen [AA]%sf(0) at %(_time) --\n%sf('306')%spc(0)AA:...\nAA:oh! hello there!\w6\nAA:is this another dream bubble ive been to so many\w6\nAA: this doesnt feel like one\w6\nAA: have we met before\n%spc(0)/
	%*\q[Yes we have!,metbefore]\n/
	%*\q[No - who are you?,notmetbefore]\n/
	%*\q[Well... (Mention Homestuck) ,tellhstuck]\n/
	\x\e"
}

OnBoot
{
	setBootVars
	
	EVAL("%(setlang)Boot")
}

enBoot
{
	"\0\s[0]%sb(0)%sf('306')"
	--
	if 0 != 0 //RED TODO: time check for when aradia was last opened vs how long shes been gone on her end
	{
		"%cb('AA')havent seen you in awhile\n"
		"%cb('AA')hi again!"
	}
	else
	{
		"%cb('AA')hi"
		"%cb('AA')hello"
		"%cb('AA')hey"
		--
		"!"
		"\i[2]\i[0] :D"
		"!\i[2]\i[0] :D"
	}
	--
	"\x\e"
}

Select.metbefore
{
	EVAL("%(setlang)MetBefore")
}

Select.notMetBefore
{
	EVAL("%(setlang)NotMetBefore")
}

Select.tellhstuck
{
	EVAL("%(setlang)Tellhstuck")
}

enMetBefore
{
	"\s[0]%sb(1)%sf('306')%cb('AA')i dont remember you myself\n%cb('AA')maybe you met an alternate version of me\n%cb('AA')but anyways nice to meet you\n%cb('AA')im going to be looking around while im here so feel free to chat :D\x\e"
}

enNotMetBefore
{
	"\s[0]%sb(1)%sf('306')%cb('AA')me either my name is aradia and im made of time\n%cb('AA')maid of time actually\n%cb('AA')its always nice to meet new people \n%cb('AA')and its also nice that we are both new to eachother\i[2] :D\n%cb('AA')im going to be looking around while im here so feel free to chat\x\e"
}

enTellhstuck
{
	"\s[0]%sb(1)You tell Aradia about the comic\n'Homestuck'.%spc(0)%sf('306')\n%cb('AA')interesting!\n%cb('AA')so you dont know me but you have heard about me\nAA: nice to meet you! \n%cb('AA')im going to be looking around while im here so feel free to chat\i[2] :D\x\e"
}

//************************************************************************
//							CLOSE FUNCTIONS
//****************************************************************

OnClose
{
	EVAL("%(setlang)Close")
}

enClose
{
	"\0\s[0]%sb(0)see you!\w8\e"
	"\0\s[0]%sb(0)bye bye!\w8\e"
	"\0\s[0]%sb(0)bye!\w8\e"
	"\0\s[0]%sb(0)till next time!\w8\e"
	"\0\s[0]%sb(0)see you later!\w8\e"
}

//************************************************************************
//							NETWORK/installation FUNCTIONS
//****************************************************************

On_homeurl
{
	"https://raw.githubusercontent.com/vermilionDev/ukagaka-HS-AA/main/src/rd_aradia_megido/"
}

OnInstallRefuse
{
	"Install refused.\e"
}

//Creating an updates2.dau
OnUpdatedataCreating
{
	"\0\s[0]Updates2.dau being created.\e"
}

//Finished creating
OnUpdatedataCreated
{
	"\0\s[0]Updates2.dau created.\e"
}

//Creating a .nar
//reference0 is the name of what you're making a nar for, reference2 is what you're making (balloon/ghost/etc)
OnNarCreating
{
	"\0\s[0]I am creating a .nar for your %(reference2), '%(reference0)'.\w8\e"
}

//Finished creating
//reference0 is the name of what you're making a nar for, reference2 is what you're making (balloon/ghost/etc)
OnNarCreated
{
	"\0\s[0]I have finished making a .nar for your %(reference2).\w8 It is located \_a[OnNarLocation,%(reference1)]here.\_a\e"
}

//When you click on the anchor in the OnNarCreated dialogue, this function will open the folder you created the .nar in
OnNarLocation
{
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]
	
	"\C\![open,file,%(_path)]"
}

OnUpdateCheckComplete 
{
	"update check says '%(reference0)'\e"
}


OnUpdateBegin
{
	"\0\s[0]Aradia's getting ready to check for an update.\e"
}

OnUpdateReady
{
	"\0\s[0]Aradia's ready to update.\e"
}

OnUpdateComplete
{
	"\0\s[0]Aradia's finished updating.\e"
}

OnUpdateFailure
{
	"\0\s[0]Aradia's update failed.\e"
}

//************************************************************************
//							Shortcut FUNCTIONS
//****************************************************************

//NOT a LANGUAGE translator, replaces regex-ish-selected text to other stuff for ya
OnTranslate
{
_talk = reference0
_talk = REPLACE(_talk, "Anchor 1", "\_a[anchor1]Anchor 1\_a")
_talk = REPLACE(_talk, "Anchor 2", "\_a[anchor2]Anchor 2\_a")
_talk = REPLACE(_talk, "Anchor 3", "\_a[anchor3]Anchor 3\_a")

	if reference1 == "" && reference2 == "" { //send input box : no event (ref2) , no special flag (ref1)
		EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
	}
	else { //event result from ghost
		TOSTR(_talk)
	}
}

OnAnchorSelect
{
	//checking to see if the link is a web address
	if "http://" _in_ reference0 || "https://" _in_ reference0 
	{
		"\j[%(reference0)]\e"
	}
}

OnChoiceSelect
{
	EVAL("Select.%(reference0)")
}

//set balloon 
sb
{
	if SHIORI3FW.BalloonName == "Trollian"
	{
		case _argv[0]
		{
			when 0; "\b[0%(bColour)]"
			when 1;	"\b[2%(bColour)]"
		}
	}
	else
	{
		case _argv[0]
		{
			when 0; "\b0"
			when 1; "\b2"
		}
	}
}

//set font
sf
{
	case _argv[0]
	{
		when 0; "\f[color,default]"
		when "al"; "\f[align,left]"
		when "ar"; "\f[align,right]"
		when "nb"; "\f[bold,1]" //oN
		when "fb"; "\f[bold,0]" //oFF
		
		when "000"; "\f[color,255,33,6]" 	//time FG
		when "002";	"\f[color,255,255,255]"	//space FG, also Doc Scratch
		when "004";	"\f[color,189,24,100]"	//Heart
		when "006";	"\f[color,6,255,201]"	//Mind
		when "008";	"\f[color,253,253,253]"	//Hope
		when "010";	"\f[color,156,77,173]"	//Rage
		when "012";	"\f[color,246,250,78]"	//Light
		when "014";	"\f[color,0,25,87]"		//Void
		when "016";	"\f[color,71,223,249]"	//Breath
		when "018";	"\f[color,186,16,22]"	//Blood
		when "020";	"\f[color,114,235,52]"	//Life
		when "022";	"\f[color,0,0,0]"		//Doom FG
		
		when "100";	"\f[color,183,13,14]"	//Time BG
		when "102";	"\f[color,85,20,42]"	//Heart BG
		when "104";	"\f[color,0,146,61]"	//Mind
		when "106";	"\f[color,255,222,85]"	//Hope
		when "108";	"\f[color,82,12,97]"	//Rage
		when "110";	"\f[color,240,132,12]"	//Light
		when "112";	"\f[color,16,78,162]"	//Void
		when "114";	"\f[color,67,121,230]"	//Breath
		when "116";	"\f[color,62,22,1]"		//Blood
		when "118";	"\f[color,164,151,135]"	//Life
		when "120";	"\f[color,48,104,0]"	//Doom
		
		when "200";	"\f[color,7,21,205]"	//John, Tavrisprite
		when "202";	"\f[color,181,54,218]"	//Rose, Fefetasprite
		when "204";	"\f[color,224,7,7]"		//Dave, Auto-responder, Arquiusprite
		when "206";	"\f[color,74,201,37]"	//Jade, Erisolsprite
		when "208";	"\f[color,0,213,242]"	//Jane, Nannasprite
		when "210";	"\f[color,255,111,242]"	//Roxy
		when "212";	"\f[color,242,164,0]"	//Davesprite, Dirk
		when "214";	"\f[color,31,148,0]"	//Jadesprite, Jake
		when "216";	"\f[color,241,65,239]"	//Jaspersprite
		
		when "300";	"\f[color,98,98,98]"	//Karkat (grey)
		when "302";	"\f[color,0,130,130]"	//Terezi / Latula
		when "304";	"\f[color,43,0,87]"		//Gamzee Only
		when "306"; "\f[color,161,0,0]"		//Aradia / Damara
		when "308";	"\f[color,65,102,0]"	//Nepeta / Meulin
		when "310";	"\f[color,161,80,0]"	//Tavros / Rufioh
		when "312";	"\f[color,0,86,130]"	//Vriska, Mindfang, Aranea
		when "314";	"\f[color,0,129,65]"	//Kanaya / Porrim
		when "316";	"\f[color,106,0,106]"	//Eridan / Cronus
		when "318";	"\f[color,119,0,60]"	//Feferi / Meenah
		when "320";	"\f[color,161,161,0]"	//Sollux / Mituna
		when "322";	"\f[color,255,0,0]"		//Squarewave, Karkat, Kankri, Alt Callie
		when "324";	"\f[color,108,0,218]"	//Kurloz
		
		when "400";	"\f[color,146,146,146]"	//Callie (Grey)
		when "402";	"\f[color,50,50,50]"	//Caliborn (Grey)
		when "404";	"\f[color,46,215,58]"	//Caliborn (Green)
		
		when "500";	"\f[color,255,85,136]"	//Default Pink
		when "502";	"\f[color,194,194,194]"	//Default Grey
	}
}

//chatBalloon
cb
{
	if chatBalloon == 1
	{
		"%(_argv[0]): "
	}
}

//spacing
spc 
{
	
	case _argv[0]
	{
		when 0; "\n\n[half]"
		when 1; "\n\n"
		when 2; "\n \_l[0]\n[-100]"
	}
}

//************************************************************************
//							     AITALK
//****************************************************************

AiTalk
{
	if RAND(100) < communicateratio
	{
		StartCommunicate
	}
	else
	{
		if mikireflag == 0
		{
			if CHAIN.IDName == "" 
			{
				RandomTalk
			}
			else 
			{
				ChainTalk
			}
		}
		else
		{ 
			MikireTalk
		}
	}
}

OnLastTalk
{
	"%(lastTalk)\e"
}

RandomTalk
{
	//check state, then split off to other dialogue
	//when to check shell?
	//when to check balloon?
	//accessibility? (Quirk on/off)?
	case gstate
	{
		when "default"; lastTalk = EVAL("%(setlang)RandomTalk")
		
		//not sure if this would ever get called,
		//but probably can't hurt to have it
		others; lastTalk = EVAL("%(setlang)RandomTalk")
	}	
	
	lastTalk
}

enRandomTalk : nonoverlap_pool
{
	//get user info
	_LSPos = GETUIINDEX("lunarSway")
	_lunarSway = userInfo[_LSPos][1]
	
	{
		"\0\s[0]%sb(0)You see Aradia look around the screen"
		--
		". She seems focused on what you're doing\n(or not doing)."
		". She seems focused on your desktop background."
		". Briefly, she rests her palm against it,\nbut pulls her hand back."
		" but you can't quite tell how she feels about it."
		". You think she seems a bit curious about it."
	}
	
	{
		"\0\s[0]%sb(0)%sf('306')%cb('AA')i learned from a human that they can lose as much as 50% of their blood before dying\n%cb('AA')though humans will likely pass out before then\n%cb('AA')trolls and especially highbloods can withstand much greater bloodloss"
		"\0\s[0]%sb(0)%sf('306')%cb('AA')people can eat anything at least once\n%cb('AA')i first heard that phrase from a human\n%cb('AA')but i think the logic applies to any creature that can eat"
		"\0\s[0]%sb(0)%sf('306')%cb('AA')some corpses, like human ones, will still grow hair and nails for a short time after dying\n%cb('AA')sorry if that was innapropriate ive been told i have a death fan girl thing"
		"\0\s[0]%sb(0)%sf('306')%cb('AA')apparently some human cultures use the bones of their dead peers for tools or decoration\n%cb('AA')instead of putting the dead in a box for a corpse party"
		}
	
	"\0\s[0]%sb(0)%sf('306')%cb('AA')its nice to have a break sometimes\e"
	
	{
			"\0\s[0]%sb(0)%sf('306')%cb('AA')dream bubbles are "
			--
			"very "
			"quite "
			""
			--
			"interesting "
			"strange "
			"fun "
			"fascinating "
			--
			"\n%cb('AA')a mashup of everyone's memories both dead and living\n%cb('AA')the environments that form can seem to be new places altogether"
			"\n%cb('AA')ive been exploring them for sweeps\n%cb('AA')and i always seem to find something new going on"
	}
	
	if _lunarSway == "prospit"
	{
		"\0\s[0]%sb(0)%sf('306')%cb('AA')having the ability to see skaias prophecies has always made dersite carpacians jealous and hateful of prospitans\w8\n%cb('AA')or perhaps they are cloned to behave that way like an instinct of sorts\w8\e"
		
		{
		"\0\s[0]%sb(0)%sf('306')%cb('AA')prospitan carpacians are very accepting and forgiving\w8\n%cb('AA')almost to the point of being doormats"
		--
		""
		"\w8\n%cb('AA')though some can be sticklers for making sure mail gets delivered"
		--
		"\w8\e"
		}
	}
	elseif _lunarSway == "both"
	{
		"\0\s[0]%sb(0)%sf('306')%cb('AA')having a dream self on both moons can seem quite advantagous\w8\n%cb('AA')for sollux it though the voices from the veil bothered him\w8\e"
		
		"\0\s[0]%sb(0)%sf('306')%cb('AA')it can seem unfair to other players that someone who has a dream self on both moons\w8\n%cb('AA')but no matter how many lives you have it wont stop a rogue agent from teleport-murdering everyone in a session\w8\e"
	}
	elseif _lunarSway == "neither"
	{
		"\0\s[0]%sb(0)%sf('306')%cb('AA')i wonder what kind of person you have to be to not be given a place on either prosit or derse\w8\e"
		
		"\0\s[0]%sb(0)%sf('306')%cb('AA')since there can be players without a prospit or derse leaning\n%cb('AA')i wonder if theres other things players could not have and yet succeed just fine\w8\e"
	}
}
